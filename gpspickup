#!/usr/bin/perl
#############################################################################################
# Monitor for new inbound GPS data files from Permanent GNSS Stations.
# When a new file arrived, move it to $SAVEDIR and unpack it to $WORKDIR.
# Files are renamed to RINEXv3 standard naming during unpack.
#
# Soren Juul Moller, Dec 2019

use strict;
use warnings;
use threads;
use Thread::Queue;
use BaseConfig;
use Logger;
use Utils;
use Job;

my $Debug = 0;

my $UQ = Thread::Queue->new;	# Unpack queue
my $PQ = Thread::Queue->new;	# Pending queue


##########################################################################################
# Extract date and time from a filename
# Returns a hash reference with info or undef if not recognized.
#
sub parseFilename($) {
  my $fn = shift;
  my ($site, $site4, $year, $yy, $doy, $mm, $dd, $hour, $hh, $mi, $ftyp, $period);
  study($fn);

  # Septentrio: sssssssss_R_yyyydddhhmi_##H_##S_MO.rnx
  #             sssssssss_R_yyyydddhhmi_##H_xN.rnx
  if ($fn =~ /^([A-Z0-9]{9})_R_([0-9]{4})([0-9]{3})([0-9]{2})([0-9]{2})_[0-9]{2}(H|D)/i) {
    ($site, $year, $yy, $doy, $hh, $hour, $mi, $period) =
		(uc($1), int($2), year2sy($2), int($3), int($4), hour2letter($4), int($5), $6);
    $hour = '0' if $period eq 'D';
    $ftyp = 'septentrio-rnx3';
    ($year, $mm, $dd) = Doy_to_Date($year, $doy);
  }

  # Septentrio raw: ssssdddh.yy_
  elsif ($fn =~ /^([A-Z0-9]{4})([0-9]{3})([a-z])\.([0-9]{2})_/i) {
    ($site4, $doy, $hour, $hh, $yy, $year, $mi) =
		(uc($1), int($2), $3, letter2hour($3), int($4), sy2year($4), 0);
    $ftyp = 'septentrio-raw';
    ($year, $mm, $dd) = Doy_to_Date($year, $doy);
  }

  # Trinzic: ssssyyyymmddhhmiB.zip
  elsif ($fn =~ /^([a-z0-9]{4})([0-9]{4})([0-9]{2})([0-9]{2})([0-9]{2})([0-9]{2})B\.zip$/i) {
    ($site4, $year, $yy, $mm, $dd, $hh, $hour, $mi) =
		(uc($1), int($2), year2sy($2), int($3), int($4), int($5), hour2letter($5), int($6));
    $ftyp = 'trinzic-zip';
    $yy = year2sy($year);
    $doy = Day_of_Year($year, $mm, $dd);
  }

  # Leica:   ssssdddh*.yyo.zip
  elsif ($fn =~ /^([a-zA-Z0-9]{4})([0-9]{3})([a-xA-X0])([0-9]+)?\.([0-9]{2})[a-z]\.zip$/i) {
    ($site4, $doy, $hour, $hh, $yy, $year) =
		(uc($1), int($2), $3, letter2hour($3), int($5), sy2year($5));
    $mi = (defined $4) ? int($4) : 0;
    $ftyp = 'leica-zip';
    ($year, $mm, $dd) = Doy_to_Date($year, $doy);
  }

  return undef if !defined $site && !defined $site4;
  if (defined $site) {
    $site4 = substr($site, 0, 4);
  } else {
    $site = $site4.'00DNK';	# best guess
  }

  return { site => $site, site4 => $site4, year => $year, doy => $doy, yy => $yy, mm => $mm, dd => $dd,
           hour => $hour, hh => $hh, mi => $mi, ftyp => $ftyp };
}

#############################################################################################
# Move all files in $src dir to $dst dir and remove $src dir
#
sub moveall($$) {
  my ($src,$dst) = @_;

  opendir(my $dh, $src) || return;
  my @files = grep(!/^\./, readdir($dh));
  closedir($dh);
  foreach my $f (@files) {
    my $s = "$src/$f";
    my $d = "$dst/$f";
    unlink($d);
    rename($s, $d)
  }
  rmdir($src);
}

#############################################################################################
#############################################################################################
# Pending thread
#
# This thread gather files to be submitted later. The file is added to its RinexSet
# object, and when no files has been added the last 20s and no active uploads in $INCOMING,
# then the job is submitted if state allows it.
# If a file arrives much later (after it has been processed), the RinexSet will be updated
# and the job will be resubmitted unless the dayjob has started.
# If the dayjob has started already, then the file will be ignored and the day needs to
# be manually reprocessed.
#

my %Pending;

sub submit_pending() {
  my @pending = keys %Pending;

  foreach my $ident (@pending) {
    my $rs = $Pending{$ident};

    # Check what we got so far. We need the obs file and at least one nav file.
    my @k = keys %$rs;
    my $have_obs = scalar(grep(/^MO\./, @k)) > 0;
    my $have_nav = scalar(grep(/^[A-Z]N$/, @k)) > 0;
    unless ($have_obs && $have_nav) {
      $rs->{'_timestamp'} = time();	# Not enough. Postpone another 20s.
      next;
    }

    # Check age. Skip if it is too young. Delete if it is stale.
    if (!defined $rs->{'MN'}) {		# We know the RinexSet is complete if it has Mixed Nav.
      my $mod_age = time() - $rs->{'_timestamp'};
      my $cre_age = time() - $rs->{'_timecreated'};
      if ($cre_age > 7200) {	# Stale?
        logwarn("Removed stale pending job $ident");
        delete $Pending{$ident};
        next;
      }
      next if $mod_age < 20;	# Too young?
    }

    # Check if something belonging to this RinexSet is being transferred right now
    my $prefix = $rs->getFilenamePrefix();
    if (scalar(grep(/^$prefix/, dirlist($INCOMING))) > 0) {
      $rs->{'_timestamp'} = time();
      next;
    }

    my $workdir = $rs->getWorkdir();
    my $unpackdir = "$workdir/unpack.".$rs->{'hour'};

    # Check if dayjob has started already.
    if ($rs->{'hour'} ne '0') {
      if (readfile("$workdir/state.0") =~ /^(queued|running)/) {
	logwarn("$ident: Cannot process. Dayjob already started. Ignoring. DOY reprocess needed.");
	delete $Pending{$ident};
	next;
      }
    }

    # Everything checks out, then submit the job if not already queued/running.
    my $job = Job->new(rs => $rs);
    my $state = $job->lockstate()->readstate();
    if ($state =~ /^(none|processed)$/) {
      loginfo("Submitting $ident");
      delete $Pending{$ident};
      moveall("$workdir/unpack.".$rs->{'hour'}, $workdir);
      $rs->store();
      $job->writestate('queued');
      $job->submitjob('ftp');
    } else {
      $rs->{'_timestamp'} = time();	# Job still active, postpone.
    }
    $job->unlockstate();
  }
}

#
# Receive file belonging to a multi-file upload. The file is added to its RinexSet
# and the timestamp is updated.
# 3sec loop ends when main threads sends the EXIT command.
#
sub pending_thread() {
  while (1) {
    submit_pending();	# Submit all ready pending

    my $parm = $PQ->dequeue_timed(3);
    next unless defined $parm && defined $parm->{'ident'};
    my $ident = $parm->{'ident'};
    last if $ident eq 'EXIT';

    my $fn = $parm->{'fn'};
    my $rs = $Pending{$ident};
    if (!defined $rs) {
      # Not in $Pending. Either new or late arrival.
      my ($site, $year, $doy, $hour) = split(/-/, $ident);
      $rs = RinexSet->new(site => $site, year => $year, doy => $doy, hour => $hour);
      if (-f $rs->getRsFile()) {
        logwarn("$ident: $fn arrived late.");
        $rs->load();
      }
    }
 
    # Add this file to this RinexSet
    if ($fn =~ /_([0-9]{2})S_MO\.rnx$/) {
      my $interval = int($1);
      $rs->{'MO.'.$interval} = $fn;
      $rs->{'interval'} = $interval;
    }
    elsif ($fn =~ /_([A-X]N)\.rnx$/) {
      $rs->{$1} = $fn;
    }
    push(@{$rs->{'origs'}}, $parm->{'ifn'});

    $rs->{'_timecreated'} = $rs->{'_timestamp'} = time();
    $Pending{$ident} = $rs;
  }

  threads->detach;
}


#############################################################################################
#############################################################################################
# Unpack thread(s)
# The heavy lifting (decompressing) is done in multiple threads.
# If the file is part of a multi-file upload, send the file to pending_thread to be added
# to its RinexSet.
#

#################################
# Unzip $zipfile into $unpackdir using RINEXv3 filenames
#
sub unzip($$$$) {
  my ($rs, $zipfile, $unpackdir, $fninfo) = @_;
  my %v3typemap = ( 'o' => 'MO', 'n' => 'GN', 'g' => 'RN', 'l' => 'EN', 'f' => 'CN', 'q' => 'JN' );

  if (sysrun([qw(/usr/bin/unzip -o -j -qq), $zipfile, '-d', $unpackdir], { log => $Debug })) {
    logwarn("unable to unzip zipfile $zipfile");
    return undef;
  }
  foreach my $zmfn (dirlist($unpackdir)) {
    if ($zmfn =~ /\.(gz|Z)$/) {				# uncompress
      sysrun(['/usr/bin/gunzip', "$unpackdir/$zmfn"]);
      $zmfn =~ s/\.(gz|Z)$//;
    }
    if ($zmfn =~ /\.(\d\dd|crx)$/) {			# crx2rnx
      my $f = $zmfn;
      $f =~ s/d$/o/;
      $f =~ s/\.crx$/.rnx/;
      sysrun([$CRX2RNX, "$unpackdir/$zmfn", "$unpackdir/$f"]);
      $zmfn = $f;
    }
    my $ftyp;
    if ($zmfn =~ /\.\d\d([onglfq])$/) {			# rename to v3 name
      $ftyp = $v3typemap{$1};
    } elsif ($zmfn =~ /_(MO|[A-Z]N)\.rnx$/) {
      $ftyp = $1;
    } else {
      logwarn($rs->getIdent().": unrecognized filetype $zmfn in $zipfile");
      unlink($zmfn);
      next;
    }
    $ftyp .= '.'.$rs->{'interval'} if $ftyp eq 'MO';
    my $ofn = $rs->getRinexFilename($ftyp, $fninfo->{'mi'});
    if ($ofn ne $zmfn) {
      unlink("$unpackdir/$ofn");
      rename("$unpackdir/$zmfn", "$unpackdir/$ofn");
    }
    $rs->{$ftyp} = $ofn;
  }
  $rs->{zipfile} = $zipfile;
  return $rs;
}


#################################
# Unpack one file into its workdir.
#
sub unpack_file($) {
  my $parm = shift;
  my ($ifn, $site, $obsint) = ($parm->{'ifn'}, $parm->{'site'}, $parm->{'obsint'});
  my $fn = basename($ifn);

  my $fninfo = parseFilename($fn);
  if (!defined $fninfo) {
    logwarn("Cannot recognize file $fn");
    return;
  }
  if ($fninfo->{'mi'} != 0) {
    logwarn("$fn: Cannot handle sub-hour files. Manual splice needed.");
    return;
  }

  my ($year, $doy, $hour) = ($fninfo->{'year'}, $fninfo->{'doy'}, $fninfo->{'hour'});

  my $job = Job->new(site => $site, year => $year, doy => $doy, hour => $hour, interval => $obsint);
  my $workdir = $job->getWorkdir;
  my $ident = $job->getIdent();
  my $rs;

  my $unpackdir = "$workdir/unpack.$hour";
  mkdir($unpackdir) unless -d $unpackdir;

  # Multi file uploads go to the pending thread. State is handled there.

  if ($fninfo->{'ftyp'} eq 'septentrio-rnx3') {
    $fn =~ s/\.gz$//;
    my $dst = "$unpackdir/$fn";
    loginfo("gunzip $fn.gz") if !$Debug;
    sysrun("/usr/bin/gzip -dc $ifn >$dst 2>/dev/null", { log => $Debug });
    $PQ->enqueue({ ident => $ident, fn => $fn, ifn => $ifn });
    return;
  }

  # Single file uploads. Unpack and submit job directly.

  if ($fninfo->{'ftyp'} eq 'septentrio-raw') {
    $rs = RinexSet->new(site => $site, year => $year, doy => $doy, hour => $hour,
			interval => $obsint, origs => [$ifn]);
    loginfo("Unpack Septentrio raw file $fn into $workdir");
    my $countrycode = substr($site, 6, 3);
    my $sbf = $ifn;
    if ($ifn =~ /\.gz$/) {
      $fn =~ s/\.gz$//;
      $sbf = "$unpackdir/$fn";
      sysrun("gzip -dc $ifn >$sbf", { log => $Debug });
    }
    chdir($unpackdir);
    sysrun("$SBF2RIN -f $sbf -l -R3 -nOP -s -D -O $countrycode >$workdir/sbf2rin.$hour.log 2>&1", { log => $Debug });
    unlink($sbf) unless $sbf eq $ifn;
    $rs->findfiles($unpackdir);
    chdir($INCOMING);
  }

  elsif ($fninfo->{'ftyp'} eq 'trinzic-zip' ||
         $fninfo->{'ftyp'} eq 'leica-zip') {
    $rs = RinexSet->new(site => $site, year => $year, doy => $doy, hour => $hour,
			interval => $obsint, zipfile => $ifn, origs => [$ifn]);
    loginfo("Unzipping $fn to $workdir");
    unzip($rs, $ifn, $unpackdir, $fninfo);
  }

  my $state = $job->lockstate()->readstate();
  if (defined $rs) {
    # Check if dayjob has started
    if ($rs->{'hour'} ne '0' && readfile("$workdir/state.0") =~ /^(queued|running)/) {
      logwarn("$ident: Cannot process. Dayjob already started. Ignoring.");
    }
    else {
      # Move files un unpack.$h to $workdir.
      if ($state =~ /^(none|processed)$/) {
        moveall($unpackdir, $workdir);
        $job->writestate('queued');
        $rs->store();
        $job->{'rsfile'} = $rs->getRsFile;
        $job->submitjob('ftp');
      } else {
        logwarn("$ident: Cannot process. Hour already being processed. Ignoring.");
      }
    }
  } else {
    $job->writestate('none');
    logerror("Unknown filetype: $fninfo->{'ftyp'}");
  }
  $job->unlockstate();
}

#
# Receive files to unpack from main thread. Loop ends when main thread calls $UQ->end()
# There will be Nthreads number of instances of this function.
#
sub unpack_thread() {
  while (my $parm = $UQ->dequeue()) {
    eval {
      unpack_file($parm);
      1;
    } || do {
      logerror("FATAL ERROR: ".longmess($parm->{'site'}.'-'.$parm->{'year'}.'-'.$parm->{'doy'}.'-'.$parm->{'hour'}));
    }
  }
  threads->detach;
}


#############################################################################################
#############################################################################################
# Main thread
# THe main thread monitors $INCOMING for new files.
# Check if site is know, move it away from $INCOMING, and send message to a unpack_thread
# to do the unpacking into $WORKDIR.
#
# IMPORTANT: All database (DBI) operations MUST be in the main thread. DBI is not thread safe.
#

use Linux::Inotify2;
use GPSDB;
use Getopt::Std;

my %SCS;
my $DB;

#
# Lookup site in gpssums. Returns true if current year-doy exists, false if not.
#
sub dailysum_exists($$$) {
  my ($site, $year, $doy) = @_;

  my $href = $DB->{'DBH'}->selectrow_hashref(q{
	select count(site) cnt
	from gpssums
	where site = ? and year = ? and doy = ? and hour = '0'
  }, undef, $site, $year, $doy);
  if (defined $href && $href->{'cnt'}) {
    logerror("$site-$year-$doy already processed. Forget DOY $site-$year-$doy first.");
    return 1;
  }
  return 0;
}


#
# Load all sites from locations table into %SCS
#
sub load_sites() {
  # Get the complete list of defined sites from DB
  # We only use the short version of sitename here.
  my $dbh = $DB->{'DBH'};
  my $aref = $dbh->selectall_arrayref(q{
	select	site, shortname, obsint
	from	locations
  }, { Slice => {} });
  die("No sites defined!?") if !defined $aref || scalar(@$aref) == 0;
  %SCS = map { $_->{'shortname'} => $_ } @$aref;
}


#
# Handle one file in $INCOMING
# Move to $SAVEDIR, create $workdir and send to unpack threads
#
sub handle_inbound($) {
  my $ifn = shift;

  my $fn = basename($ifn);
  my $fninfo = parseFilename($fn);
  if (!defined $fninfo || !defined $fninfo->{'site4'}) {
    logwarn("$fn not recorgnized. Moved to stale.");
    sysmv([$ifn], $SAVEDIR.'/stale', { log => $Debug, mkdir => 1 });
    return;
  }

  # Make sure we know the site
  my $sc = $SCS{$fninfo->{'site4'}};
  if (!defined $sc) {
    logwarn('No such site: '.$fninfo->{'site4'}.'. Moved to stale.');
    sysmv([$ifn], $SAVEDIR.'/stale', { log => $Debug, mkdir => 1 });
    return;
  }

  # From hereon use the 9-ch site name.
  my ($site, $year, $doy) = ($sc->{'site'}, $fninfo->{'year'}, $fninfo->{'doy'});
  my $savedir = sprintf("%s/%s/%4d/%03d", $SAVEDIR, $site, $year, $doy);
  sysmv([$ifn], $savedir, { log => $Debug, mkdir => 1 });

  my $job = Job->new(site => $site, year => $year, doy => $doy, hour => $fninfo->{'hour'}, interval => $sc->{'obsint'});
  my $workdir = $job->getWorkdir();
  if (! -d $workdir) {
    return if dailysum_exists($site, $year, $doy);
    $job->mkWorkdir();
  }
  $UQ->enqueue({ ifn => "$savedir/$fn", site => $site, obsint => $sc->{'obsint'} });
}


#
# scan_incoming will add all plain files in $INCOMING to %Inbound
#
sub scan_incoming() {
  my $fd;
  open($fd, '-|', qq(/bin/find $INCOMING -type f -print));
  while (<$fd>) {
    chomp;
    handle_inbound($_) if fileage($_) > 20;
  }
  close($fd);
}

# Below the actual main

setprogram(basename($0));
$0 = basename($0)." ".join(' ',@ARGV);

my %opts = ();
getopts('c:di:l:', \%opts);

BaseConfig::init($opts{'c'}) if defined $opts{'c'};

$Debug = 1 if defined $opts{'d'};
logchannel($opts{'l'}) if defined $opts{'l'};
my $Nthreads = (defined $opts{'i'} ? $opts{'i'} : $JOBINSTANCES);

chdir($INCOMING);
select(STDERR); $| = 1;
select(STDOUT); $| = 1;

# start threads
threads->create('unpack_thread') for (1..$Nthreads);
threads->create('pending_thread');

my $inotify = Linux::Inotify2->new()
	|| errdie("$0: unable to create new inotify object: $!");
$inotify->blocking(0);
$inotify->watch($INCOMING, IN_MOVED_TO | IN_CLOSE_WRITE);

$DB = GPSDB->new;	# GPSDB must be instantiated after thread creations
load_sites();

loginfo("$0 started. threads=$Nthreads, Debug=$Debug");

scan_incoming();

my $signal_received = '';
local $SIG{INT} = sub { $signal_received = 'interrupted' };
local $SIG{TERM} = sub { $signal_received = 'terminated' };
local $SIG{HUP} = sub { $signal_received = 'SIGHUP' };

# %inbound is used for delaying the processing of an inbound file by at least 1s.
# This because uploaders may want to set attributes after file has been uploaded,
# and if moved straigt away, this will fail. It also ensures that inbound filenames
# are unique (filter out multiple events for the same file).

my %inbound;

my $ntimeouts = 0;
while (!$signal_received) {

  foreach (sort keys %inbound) {
    if (time() - $inbound{$_} > 1) {
      handle_inbound($_);
      delete $inbound{$_};
    }
  }

  my $v = '';
  vec($v, $inotify->fileno, 1) = 1;
  select($v, undef, undef, 1);			# wait 1s for inotify has something
  if (vec($v, $inotify->fileno, 1)) {
    $ntimeouts = 0;
    my @events = $inotify->read;
    $inbound{$_->fullname} = time() foreach @events;
  } elsif (scalar(keys %inbound) == 0) {
    $ntimeouts++;
    if ($signal_received eq 'SIGHUP' || $ntimeouts % 600 == 0) {
      load_sites();
      scan_incoming();
    }
  }
  $signal_received = '' if $signal_received eq 'SIGHUP';
}

# shutdown threads
$PQ->enqueue({ ident => 'EXIT' });
$UQ->end();

my $nwaits = 0;
sleep 1 while ($nwaits++ < 25 && threads->list(threads::running));	# wait for threads to terminate

loginfo("$0 stopped");
logclose();
exit(0);
